
    model{

    ## Priors
    ##hyperprior for intercept alpha
    flow.mu~dnorm(0,0.001)   
    fert.mu~dnorm(0,0.001)   
    grow.mu~dnorm(0,0.001)   
    surv.mu~dnorm(0,0.001) 
    
    ## plot variances
    flow.sigma.plot~dunif(0,1000)
    flow.tau.plot<-1/(flow.sigma.plot*flow.sigma.plot)
    fert.sigma.plot~dunif(0,1000)
    fert.tau.plot<-1/(fert.sigma.plot*fert.sigma.plot)
    grow.sigma.plot~dunif(0,1000)
    grow.tau.plot<-1/(grow.sigma.plot*grow.sigma.plot)
    surv.sigma.plot~dunif(0,1000)
    surv.tau.plot<-1/(surv.sigma.plot*surv.sigma.plot)
    
    ## year variances
    flow.sigma.year~dunif(0,1000)
    flow.tau.year<-1/(flow.sigma.year*flow.sigma.year)
    fert.sigma.year~dunif(0,1000)
    fert.tau.year<-1/(fert.sigma.year*fert.sigma.year)
    grow.sigma.year~dunif(0,1000)
    grow.tau.year<-1/(grow.sigma.year*grow.sigma.year)
    surv.sigma.year~dunif(0,1000)
    surv.tau.year<-1/(surv.sigma.year*surv.sigma.year)
    
    ## overdispersion variance for fertility (NB approximation)
    fert.sigma.overdisp~dunif(0,1000)
    fert.tau.overdisp<-1/(fert.sigma.overdisp*fert.sigma.overdisp)
    
    ## residual sigma as a linear function of size (these are the size regression coefs)
    grow.sigma.eps_b0 ~ dnorm(0,0.001)
    grow.sigma.eps_b1 ~ dnorm(0,0.001)

    ## variable selection priors for climate effects
    ## looping over coefficients (i) and PC variables (j)
    ## Here each coefficient has its own indicator variable
    ## for(i in 1:4){
    for(i in 1:3){
    for(j in 1:3){
    flow.bclim[i,j] ~ dnorm(0,flow.prec[i,j])
    flow.prec[i,j] <- 1/flow.V[i,j]
    flow.V[i,j] <- (1-flow.z[i,j])*0.001 + flow.z[i,j]*10
    flow.z[i,j] ~ dbern(0.5)
    
    fert.bclim[i,j] ~ dnorm(0,fert.prec[i,j])
    fert.prec[i,j] <- 1/fert.V[i,j]
    fert.V[i,j] <- (1-fert.z[i,j])*0.001 + fert.z[i,j]*10
    fert.z[i,j] ~ dbern(0.5)
    
    grow.bclim[i,j] ~ dnorm(0,grow.prec[i,j])
    grow.prec[i,j] <- 1/grow.V[i,j]
    grow.V[i,j] <- (1-grow.z[i,j])*0.001 + grow.z[i,j]*10
    grow.z[i,j] ~ dbern(0.5)
    
    surv.bclim[i,j] ~ dnorm(0,surv.prec[i,j])
    surv.prec[i,j] <- 1/surv.V[i,j]
    surv.V[i,j] <- (1-surv.z[i,j])*0.001 + surv.z[i,j]*10
    surv.z[i,j] ~ dbern(0.5)
    }
    }
    ## prior for size slope - also do SVS
    flow.bsize~dnorm(0,flow.precsize) 
    flow.precsize <- 1/flow.Vsize
    flow.Vsize <- (1-flow.zsize)*0.001 + flow.zsize*10
    flow.zsize ~ dbern(0.5)

    fert.bsize~dnorm(0,fert.precsize) 
    fert.precsize <- 1/fert.Vsize
    fert.Vsize <- (1-fert.zsize)*0.001 + fert.zsize*10
    fert.zsize ~ dbern(0.5)

    grow.bsize~dnorm(0,grow.precsize) 
    grow.precsize <- 1/grow.Vsize
    grow.Vsize <- (1-grow.zsize)*0.001 + grow.zsize*10
    grow.zsize ~ dbern(0.5)

    surv.bsize~dnorm(0,surv.precsize) 
    surv.precsize <- 1/surv.Vsize
    surv.Vsize <- (1-surv.zsize)*0.001 + surv.zsize*10
    surv.zsize ~ dbern(0.5)
    
    ##plot and year deviates on the intercept
    for(i in 1:N.plots){      
    flow.eps.plot[i]~dnorm(0,flow.tau.plot)
    fert.eps.plot[i]~dnorm(0,fert.tau.plot)
    grow.eps.plot[i]~dnorm(0,grow.tau.plot)
    surv.eps.plot[i]~dnorm(0,surv.tau.plot)
    }
    for(i in 1:N.years){      
    flow.eps.year[i]~dnorm(0,flow.tau.year)
    fert.eps.year[i]~dnorm(0,fert.tau.year)
    grow.eps.year[i]~dnorm(0,grow.tau.year)
    surv.eps.year[i]~dnorm(0,surv.tau.year)
    }
    
    ## Likelihoods
    
    ## Flowering
    for(i in 1:flow.N.obs){
    logit(flow.p[i]) <- flow.mu + flow.eps.plot[flow.plot[i]] + flow.eps.year[flow.year[i]] + flow.bsize*flow.size[i]+ 
    flow.bclim[1,1]*flow.PC1[i] + 
    flow.bclim[2,1]*pow(flow.PC1[i],2) + 
    flow.bclim[3,1]*flow.PC1[i]*flow.size[i] + 
    ##flow.bclim[4,1]*pow(flow.PC1[i],2)*flow.size[i] + 
    flow.bclim[1,2]*flow.PC2[i] + 
    flow.bclim[2,2]*pow(flow.PC2[i],2) + 
    flow.bclim[3,2]*flow.PC2[i]*flow.size[i] + 
    ##flow.bclim[4,2]*pow(flow.PC2[i],2)*flow.size[i] + 
    flow.bclim[1,3]*flow.PC3[i] + 
    flow.bclim[2,3]*pow(flow.PC3[i],2) + 
    flow.bclim[3,3]*flow.PC3[i]*flow.size[i]
    ## + flow.bclim[4,3]*pow(flow.PC3[i],2)*flow.size[i]
    
    flow.y[i]~dbern(flow.p[i])
    
    flow.Presi[i] <- abs(flow.y[i]-flow.p[i])
    flow.y.new[i] ~ dbern(flow.p[i])
    flow.Presi.new[i] <- abs(flow.y.new[i] - flow.p[i])
    }

    ## Survival (same model as flowering)
    for(i in 1:surv.N.obs){
    logit(surv.p[i]) <- surv.mu + surv.eps.plot[surv.plot[i]] + surv.eps.year[surv.year[i]] + surv.bsize*surv.size[i]+ 
    surv.bclim[1,1]*surv.PC1[i] + 
    surv.bclim[2,1]*pow(surv.PC1[i],2) + 
    surv.bclim[3,1]*surv.PC1[i]*surv.size[i] + 
    ##surv.bclim[4,1]*pow(surv.PC1[i],2)*surv.size[i] + 
    surv.bclim[1,2]*surv.PC2[i] + 
    surv.bclim[2,2]*pow(surv.PC2[i],2) + 
    surv.bclim[3,2]*surv.PC2[i]*surv.size[i] + 
    ##surv.bclim[4,2]*pow(surv.PC2[i],2)*surv.size[i] + 
    surv.bclim[1,3]*surv.PC3[i] + 
    surv.bclim[2,3]*pow(surv.PC3[i],2) + 
    surv.bclim[3,3]*surv.PC3[i]*surv.size[i]
    ## + surv.bclim[4,3]*pow(surv.PC3[i],2)*surv.size[i]
    
    surv.y[i]~dbern(surv.p[i])
    
    surv.Presi[i] <- abs(surv.y[i]-surv.p[i])
    surv.y.new[i] ~ dbern(surv.p[i])
    surv.Presi.new[i] <- abs(surv.y.new[i] - surv.p[i])
    }
    
    ## Fertility
    for(i in 1:fert.N.obs){
    fert.eps.overdisp[i] ~ dnorm(0,fert.tau.overdisp)
    
    log(fert.lambda[i]) <- fert.mu + fert.eps.plot[fert.plot[i]] + fert.eps.year[fert.year[i]] + fert.eps.overdisp[i] + 
    fert.bsize*fert.size[i]+ 
    fert.bclim[1,1]*fert.PC1[i] + 
    fert.bclim[2,1]*pow(fert.PC1[i],2) + 
    fert.bclim[3,1]*fert.PC1[i]*fert.size[i] + 
    ##fert.bclim[4,1]*pow(fert.PC1[i],2)*fert.size[i] + 
    fert.bclim[1,2]*fert.PC2[i] + 
    fert.bclim[2,2]*pow(fert.PC2[i],2) + 
    fert.bclim[3,2]*fert.PC2[i]*fert.size[i] + 
    ##fert.bclim[4,2]*pow(fert.PC2[i],2)*fert.size[i] + 
    fert.bclim[1,3]*fert.PC3[i] + 
    fert.bclim[2,3]*pow(fert.PC3[i],2) + 
    fert.bclim[3,3]*fert.PC3[i]*fert.size[i]
    ## + fert.bclim[4,3]*pow(fert.PC3[i],2)*fert.size[i]
    
    fert.y[i]~dpois(fert.lambda[i])
    
    fert.Presi[i] <- (fert.y[i]-fert.lambda[i])/sqrt(fert.lambda[i])
    fert.y.new[i] ~ dpois(fert.lambda[i])
    fert.Presi.new[i] <- (fert.y.new[i]-fert.lambda[i])/sqrt(fert.lambda[i])
    fert.D[i] <- pow(fert.Presi[i],2)
    fert.D.new[i] <- pow(fert.Presi.new[i],2)

    }    
    
    ## Growth 
    for(i in 1:grow.N.obs){
    grow.mean[i] <- grow.mu + grow.eps.plot[grow.plot[i]] + grow.eps.year[grow.year[i]] + grow.bsize*grow.size[i]+ 
    grow.bclim[1,1]*grow.PC1[i] + 
    grow.bclim[2,1]*pow(grow.PC1[i],2) + 
    grow.bclim[3,1]*grow.PC1[i]*grow.size[i] + 
    ##grow.bclim[4,1]*pow(grow.PC1[i],2)*grow.size[i] + 
    grow.bclim[1,2]*grow.PC2[i] + 
    grow.bclim[2,2]*pow(grow.PC2[i],2) + 
    grow.bclim[3,2]*grow.PC2[i]*grow.size[i] + 
    ##grow.bclim[4,2]*pow(grow.PC2[i],2)*grow.size[i] + 
    grow.bclim[1,3]*grow.PC3[i] + 
    grow.bclim[2,3]*pow(grow.PC3[i],2) + 
    grow.bclim[3,3]*grow.PC3[i]*grow.size[i]
    ## + grow.bclim[4,3]*pow(grow.PC3[i],2)*grow.size[i]
    
    log(grow.sig2[i]) <- grow.sigma.eps_b0 + grow.sigma.eps_b1 * grow.size[i]

    grow.y[i]~dnorm(grow.mean[i],(1/grow.sig2[i]))
    
    grow.sq.res[i] <- pow(grow.y[i] - grow.mean[i],2)
    grow.y.new[i] ~ dnorm(grow.mean[i], (1/grow.sig2[i]))
    grow.sq.res.new[i] <- pow(grow.y.new[i] - grow.mean[i], 2)
    }    
    
    
    ## sum up posterior predictive checks
    flow.fit <- sum(flow.Presi[]) 
    flow.fit.new <- sum(flow.Presi.new[]) 
    surv.fit <- sum(surv.Presi[]) 
    surv.fit.new <- sum(surv.Presi.new[]) 
    fert.fit <- sum(fert.D[]) 
    fert.fit.new <- sum(fert.D.new[])
    grow.fit <- sum(grow.sq.res[]) 
    grow.fit.new <- sum(grow.sq.res.new[])
    
    }##end model
    
